config {
  type: "table",
  schema: dataform.projectConfig.vars.RAW_DATA,
  name: "core_merchants",
  tags: ["core_merchants_incr_dim"]
}
js {
  function TableName(p) {
    return "'"+p.split(".")[2].replace("`","")+"'";
  }
}

pre_operations {

  DECLARE current_timestamp_now  DEFAULT (  
    SELECT CURRENT_TIMESTAMP() AS now
  );

  DECLARE last_refresh_timestamp DEFAULT (  
    SELECT MAX(LastRefreshTimestamp) FROM ${ resolve({ schema: dataform.projectConfig.vars.RAW_DATA, name:  dataform.projectConfig.vars.CONTROL_TABLE}) }
    WHERE TableName = ${TableName(`${self()}`)}
  );

  UPDATE ${ resolve({ schema: dataform.projectConfig.vars.RAW_DATA, name:  dataform.projectConfig.vars.CONTROL_TABLE}) }
  SET LastRefreshTimestamp = current_timestamp_now, LastRefreshStatus = 'PROCESSING'
    WHERE TableName = ${TableName(`${self()}`)}
}

SELECT
    merchant_id,
    merchant_name,
    mcc,
    email,
    street,
    city,
    state,
    country,
    zip,
    latitude,
    longitude,
    owner_id,
    owner_name,
    terminal_ids,
    Description,
    Market_Segment,
    Industry_Code_Description,
    Industry_Code,
    ingest_date,
    _PARTITIONTIME as LastRefreshTimestamp
FROM
  ${ ref({ schema: dataform.projectConfig.vars.RAW_DATA, name: "core_merchants_stg" }) }
WHERE _PARTITIONTIME BETWEEN last_refresh_timestamp and current_timestamp_now

post_operations {
  UPDATE ${ resolve({ schema: dataform.projectConfig.vars.RAW_DATA, name:  dataform.projectConfig.vars.CONTROL_TABLE}) }
  SET LastRefreshTimestamp = current_timestamp_now, LastRefreshStatus = 'SUCCESS'
    WHERE TableName = ${TableName(`${self()}`)}
}

